import groovy.json.JsonSlurper
import groovy.json.JsonOutput

plugins {
    id 'com.github.johnrengelman.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/shadow.gradle"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

assemble {
    dependsOn shadowJar
}

classes {
    doLast {
        def pluginFile = file("$buildDir/classes/java/main/velocity-plugin.json")
        def pluginDescriptor = new JsonSlurper().parse(pluginFile)
        pluginDescriptor["version"] = version
        pluginFile.write(new JsonOutput().toJson(pluginDescriptor))
    }
}

shadowJar {
    archiveFileName = "mapmodcompanion.jar"
}

dependencies {
    implementation project(':common')
    implementation "org.bstats:bstats-velocity:3.0.0"

    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

task copyToPlugins(type: Copy) {
    from shadowJar.outputs.files.singleFile
    into "$projectDir/debug/plugins"
    dependsOn shadowJar
}

plugins {
    alias libs.plugins.spigradle
    alias libs.plugins.shadow
}

String bStats = extensions.getByType(VersionCatalogsExtension).named("libs").findLibrary("bstats-bukkit").orElseThrow().get().with {
    "${getModule()}:${getVersionConstraint()}".toString()
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/shadow.gradle"

repositories {
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

spigot {
    name = 'MapModCompanion'
    authors = ['turikhay']
    apiVersion = '1.13'
    softDepends = ['ProtocolLib']
    debug {
        jvmArgs "-Xmx2048m",
                "-Dlog4j.configurationFile=${projectDir}${File.separatorChar}log4j2-debug.xml",
                "-Dcom.turikhay.mc.mapmodcompanion.spigot.debug=true"
    }
    afterEvaluate {
        ArrayList<String> excluded = []
        excluded.addAll(rootProject.allprojects.collect { new String("${group}:${it.name}:${version}") })
        excluded += bStats
        excludeLibraries((String[]) excluded.toArray())
    }
}

dependencies {
    implementation project(':common')
    implementation bStats

    // These dependencies are intentionally not present in libs.version.toml
    compileOnly spigot(rootProject.spigot_version)
    compileOnly "com.comphenix.protocol:ProtocolLib:${rootProject.protocolLib_version}"
}

assemble {
    dependsOn shadowJar
}

shadowJar {
    archiveFileName = "MapModCompanion-Spigot.jar"
}
